plugins {
	id 'java'
	id 'application'
	id 'org.openjfx.javafxplugin' version '0.0.9'
	id 'org.beryx.jlink' version '2.22.0'
}

java {
	// jdk 14
	sourceCompatibility = JavaVersion.VERSION_14;
	targetCompatibility = JavaVersion.VERSION_14;
}

group 'org.beuwi.msgbots'

javafx {
	version = '14'
	modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web' ]
}

application {
	mainClassName = "org.beuwi.msgbots.Launcher"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation group: 'org.jsoup', name: 'jsoup', version: '1.13.1'
	implementation group: 'org.mozilla', name: 'rhino', version: '1.7.13'
	// implementation group: 'commons-io', name: 'commons-io', version: '2.6'
	implementation group: 'com.jfoenix', name: 'jfoenix', version: '9.0.10'
	implementation group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.10.5'
	// implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.6.0'
	implementation (group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1') {
		exclude group: 'org.hamcrest', module: 'hamcrest-core'
	}
}

compileJava {
	options.encoding = "UTF-8"
	options.compilerArgs << "-Xlint:none"
}

run {
	jvmArgs '--add-exports', 'javafx.base/com.sun.javafx.event=ALL-UNNAMED',
			'--add-exports', 'javafx.graphics/com.sun.javafx.iio=ALL-UNNAMED',
			'--add-exports', 'javafx.graphics/com.sun.javafx.iio.common=ALL-UNNAMED',
			'--add-exports', 'javafx.controls/com.sun.javafx.scene.control=ALL-UNNAMED'
}


jlink {
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

	launcher {
		name = 'Simulator'
	}

	jpackage {
		imageOptions += ['--icon', 'src/main/resources/icons/program.ico']

		/* installerOptions = [
				'--vendor', 'beuwi',
				'--app-version', '0.5.0',
		] */
	}
}

jar {
	manifest {
		attributes 'Main-Class': 'org.beuwi.msgbots.Launcher'
	}

	/* from {
		configurations.compile.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	} */
}